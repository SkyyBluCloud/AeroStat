VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "Form_frmChecklistsDash"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = True
Attribute VB_PredeclaredId = True
Attribute VB_Exposed = False
Option Compare Database

Private Sub btnDelete_Click()
On Error GoTo errtrap
If IsNull(lstChecklists) Then Exit Sub
Dim cName As String: cName = lstChecklists.Column(2)
Dim db As DAO.Database: Set db = CurrentDb

    If MsgBox("Delete " & cName & " checklist?", vbQuestion + vbYesNo, "Checklists") = vbYes Then
        db.Execute "DELETE FROM tblChecklistIndex WHERE checklistID = " & lstChecklists
        log "Deleted " & db.RecordsAffected & " checklist(s)", Me.Name & ".btnDelete_Click"
    End If
    lstChecklists = ""
    
sExit:
    Exit Sub
errtrap:
    ErrHandler err, Error$, Me.Name & ".btnDelete_Click"
End Sub

Private Sub btnNew_Click()
Dim db As DAO.Database: Set db = CurrentDb

    db.Execute "INSERT INTO tblChecklistIndex (title, code) SELECT 'New Checklist', '###'"
    Me.lstChecklists.Requery
    Me.lstChecklists = db.OpenRecordset("SELECT @@IDENTITY").Fields(0)
    
    With sfEditChecklist.Form
        .Recordset.AddNew
        !checklistID = lstChecklists
        !order = 1
        !item = "New Item"
        .Recordset.Update
        
        .bClose = True
        .filter = "checklistID = " & Me.lstChecklists
        .FilterOn = True
    End With
    
    refreshChecklistEditor
    
        
End Sub

Public Function refreshChecklistEditor()
On Error GoTo errtrap

    If Nz(lstChecklists) = "" Then
        Me.sfEditChecklist.SourceObject = ""
    Else
        Dim qdf As DAO.QueryDef
        Set qdf = CurrentDb.QueryDefs("qChecklistItems")
        qdf.Parameters("checklist") = lstChecklists
        
        Me.sfEditChecklist.SourceObject = "frmChecklistEdit"
        With Me.sfEditChecklist.Form
            .filter = "checklistID = " & lstChecklists
            .FilterOn = True
            Set !lstItems.Recordset = qdf.OpenRecordset
        End With
    End If
    
sExit:
    Exit Function
errtrap:
    ErrHandler err, Error$, Me.Name & ".refreshChecklistEditor"
End Function



